import win.ui;
/*DSG{{*/
mainForm = win.form(text="gpicar";right=495;bottom=273;border="dialog frame";max=false)
mainForm.add(
btnQuit={cls="button";text="关闭";left=396;top=233;right=472;bottom=257;db=1;dr=1;z=10};
btnSelect={cls="button";text="选择目录";left=408;top=35;right=472;bottom=61;dr=1;dt=1;z=3};
btnStart={cls="button";text="开始";left=315;top=233;right=389;bottom=257;db=1;dr=1;z=9};
chkEnableDebug={cls="checkbox";text="DEBUG";left=28;top=239;right=101;bottom=253;ah=1;aw=1;z=11};
chkNoArchiving={cls="checkbox";text="只改名（不归档）";left=48;top=147;right=167;bottom=165;db=1;dl=1;z=7};
chkParseVedios={cls="checkbox";text="处理视频";left=48;top=184;right=128;bottom=200;checked=1;db=1;dl=1;z=8};
chkSetPrefix={cls="checkbox";text="自定义前缀";left=48;top=112;right=128;bottom=128;dl=1;dt=1;z=5};
groupbox={cls="groupbox";text="设置";left=24;top=80;right=472;bottom=216;db=1;dl=1;dr=1;dt=1;edge=1;z=4};
lstPrefixs={cls="combobox";left=136;top=112;right=360;bottom=136;disabled=1;dl=1;dr=1;dt=1;edge=1;items={};mode="dropdown";z=6};
static={cls="static";text="选择目录";left=24;top=16;right=102;bottom=31;ah=1;aw=1;dl=1;dt=1;transparent=1;z=1};
txtFolder={cls="edit";left=24;top=35;right=400;bottom=61;dl=1;dr=1;dt=1;edge=1;multiline=1;z=2}
)
/*}}*/

buildCommandLine = function(config){
	import string.builder;

	var args = {};
	
	if config.prefix != '' {
		table.push(args, '-prefix="' + config.prefix + '"');
	}
	
	if config.setNoArchiving {
		table.push(args, "-renameonly");
	}
	
	if config.setParseVedios {
		table.push(args, "-videos");
	}
	
	if config.dir != '' {
		table.push(args, config.dir);
	}
	
	var result = string.join(args, " ");
	return result;
}

startMainExe = function(commandline){
	import fsys;
	import fsys.path;
	import process;
	
	var enableDebug = mainForm.chkEnableDebug.checked;
	
	var exe = fsys.path.append(fsys.getCurDir(), "picar.exe");
	
	if(enableDebug) {
		mainForm.msgbox('使用参数：' + commandline);
	}
	
	if(io.exist(exe)) {
		var proc = process(exe,commandline, fsys.getCurDir());
		if proc.id > 0 {
			mainForm.close();
		}
		return;
	}
	
	mainForm.msgbox("无法找到 picar.exe");
}


mainForm.btnStart.oncommand = function(id,event){	
	var prefix = '';
	if(mainForm.chkSetPrefix.checked) {
		prefix = mainForm.lstPrefixs.text;
	} else {
		prefix = 'NOPREFIX';
	}
	
	var config = {
		setNoArchiving = mainForm.chkNoArchiving.checked;
		setParseVedios = mainForm.chkParseVedios.checked;
		dir = mainForm.txtFolder.text;
		prefix = prefix;
	};
	
	if(config.dir == '') {
		mainForm.msgbox('请选择目录');
		return;
	}
	
	var commandline = buildCommandLine(config);
	startMainExe(commandline)
}

mainForm.chkSetPrefix.oncommand = function(id,event){
	mainForm.lstPrefixs.disabled = !mainForm.chkSetPrefix.checked
}

mainForm.btnQuit.oncommand = function(id,event){
	mainForm.close();	
}


mainForm.btnSelect.oncommand = function(id,event){
	import fsys.dlg;
	var path = fsys.dlg.opendir();
	if (path != '') {
		mainForm.txtFolder.text = path;
	}
}

mainForm.lstPrefixs.items = {
	""; 
	"MX6-Momo"; 
	"MX6-Lok-fung"; 
	"H60"; 
	"N9005"; 
	"iPhone7"; 
	"iPhon7s"; 
	"iPhone6s"; 
	"iPhone6s"; 
	"iPhone8p"
};
mainForm.lstPrefixs.selIndex = 0;
mainForm.btnQuit.setFocus();

mainForm.enableDpiScaling();
mainForm.show();

return win.loopMessage(); 
